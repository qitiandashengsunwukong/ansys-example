FINISH $ /CLEAR
/FILNAME, I-BEAM
/TITLE, "Buckling Analysis of I-Beam."

/PREP7
ET, 1, BEAM188
SECTYPE, 1, BEAM, I
SECOFFSET, CENT
SECDATA, 0.15, 0.15, 0.25, 0.015, 0.015, 0.015
MP, DENS, 1, 7.85E3
MP, EX, 1, 2.06E11
MP, NUXY, 1, 0.2

K, 1
K, 2, 2.5
K, 3, 1.250, 1.0
LSTR, 1, 2
LATT, 1, , 1, , 3, , 1
LESIZE, 1, , , 10
LMESH, 1
/VIEW, 1, 1, 1, 1
/REPLOT, FAST
/ESHAPE, 1
EPLOT

/SOLU
ANTYPE, STATIC
D, NODE(0.0, 0.0, 0.0), ALL
F, NODE(2.5, 0.0, 0.0), FX, -1
PSTRES, ON
SOLVE
FINISH

/SOLU
ANTYPE, BUCKLE
BUCOPT, LANB, 1
MXPAND, 1, 0, 0, 1, 0.001
SOLVE

/POST1
SET, FIRST
PLDISP, 1
FINISH

/POST1
SET, FIRST
*GET, LoadScale, ACTIVE, , SET, FREQ
FINISH

/PREP7
TB, BISO, 1, 1, 2
TBTEMP, 0
TBDATA, , 2.0E8, 0
UPGEOM, 0.01, 1, 1, 'I-BEAM', 'RST'
FINISH

!/SOLU
!ANTYPE, STATIC
!FSCALE, LoadScale
!NLGEOM, ON
!OUTRES, ALL, ALL
!ARCLEN, ON
!ARCTRM, U, 0.05, 2, UZ
!NSUBST, 200
!SOLVE
!FINISH
!
!/POST1
!SET, LAST
!PLDISP, 1
!FINISH
!
!/POST26
!NSOL, 2, 2, U, Z, DEFLECTION
!RFORCE, 3, 1, F, X, REACTIONF
!/AXLAB, X, DEFLECTION
!/AXLAB, Y, REACTIONF
!XVAR, 2
!PLVAR, 3
!FINISH

/SOLU
ANTYPE, STATIC
FSCALE, LoadScale
NLGEOM, ON
OUTRES, ALL, ALL
STABILIZE, CONSTANT, ENERGY, 1.0E-4
NSUBST, 200
SOLVE
FINISH

/POST26
NSOL, 2, 2, U, Z, DEFLECTION
RFORCE, 3, 1, F, X, REACTIONF
/AXLAB, X, DEFLECTION
/AXLAB, Y, REACTIONF
XVAR, 2
PLVAR, 3
