FINISH $ /CLEAR
/FILNAME, beam
/TITLE, "SPECTRUM ANALYSIS OF A CANTILEVER BEAM"

/PREP7
! UNITS: N-m-s

! ELEMENT TYPE
ET, 1, BEAM188
ET, 2, MASS21
KEYOPT, 2, 3, 4

! MATERIAL DEFINITION
MP, EX, 1, 3.0E10
MP, PRXY, 1, 0.2
MP, DENS, 1, 0

! SECTION
SECTYPE, 1, BEAM, RECT
SECDATA, 1, 0.15, 0.3

! REAL CONSTANTS
R, 1, 1000

! FEA MODEL: NODES
N, 1
N, 2, 1
N, 3, 2
N, 4, 3

! FEA MODEL: BEAM188 ELEMENTS
TYPE, 1 $ MAT, 1 $ SECNUM, 1
E, 1, 2
E, 2, 3
E, 3, 4

! FEA MODEL: MASS21 ELEMENTS
TYPE, 2 $ MAT, 1 $ REAL, 1
E, 2
E, 3
E, 4

! DOF CONSTRAINTS
D, 1, ALL
D, 2, UX, , , 4

FINISH

! MODAL ANALYSIS
/SOLU
ANTYPE, MODAL
MODOPT, SUBSP, 3
MXPAND, 3
SOLVE

! MODE EXTRACTION
/POST1
SET, LIST
FINISH

! SPECTRUM ANALYSIS
/SOLU
ANTYPE, SPECTRUM
SPOPT, SPRS, 10
SVTYP, 2, 1
SED, 0, 1, 0
FREQ,0.3333,0.8363,1.669,2.500,3.330,4.167,5.000,10.000,100.000
SV,0,0.2653,0.2653,0.4941,0.7108,0.9210,1.1260,1.326,1.326,0.6632
SOLVE
FINISH

! MODAL SOLUTION COMBINATION
/SOLU
ANTYPE, SPECTRUM
SRSS
SOLVE
FINISH

! POST PROCESSING
/POST1
/INPUT, 'BEAM', 'MCOM'
PLNSOL, U, Y, 0, 1
FINISH