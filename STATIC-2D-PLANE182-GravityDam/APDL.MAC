!****************************************************************************
!独立重力坝静力分析
!****************************************************************************
FINISH $ /CLEAR
! 1. 分析环境设置
/FILENAME, Gravity dam    ! 指定工作名称
/TITLE, static analysis of the gravity dam
/REPLOT

! 2. 进入前处理器
/PREP7

! 3. 定义单元类型及平面应变选项
ET, 1, PLANE182
KEYOPT, 1, 3, 2

! 4. 定义材料模型及参数
MP, EX, 1, 2.1E10     ! 混凝土材料模型
MP, PRXY, 1, 0.2
MP, DENS, 1, 2400
MP, EX, 2, 1.0E8      ! 粘性土材料模型
MP, PRXY, 2, 0.35
MP, DENS, 2, 2200
MP, EX, 3, 2.98E10    ! 花岗岩材料模型
MP, PRXY, 3, 0.17
MP, DENS, 3, 2600

! 5. 定义关键点
K, 1, 
K, 2, -56, 67
K, 3, -56, 70
K, 4, -60, 70
K, 5, -60, 67
K, 6, -60, 0
K, 7, -210, 0
K, 8, -210, -60
K, 9, -60, -22.5
K, 10, 0, -7.5
K, 11, 30, 0
K, 12, 180, 0
K, 13, 180, -140
K, 14, 30, -140
K, 15, 0, -140
K, 16, -60, -140
K, 17, -210, -140

! 6. 创建直线
L, 1, 2
L, 5, 6
L, 1, 6
L, 9, 10
L, 15, 16
L, 8, 17
L, 2, 3
L, 3, 4
L, 4, 5
L, 2, 5
L, 7, 8
L, 6, 9
L, 1, 10
L, 1, 11
L, 10, 11
L, 14, 15
L, 6, 7
L, 9, 8
L, 16, 17
L, 10, 15
L, 11, 14
L, 11, 12
L, 12, 13
L, 13, 14
L, 9, 16

! 7. 创建坝体及坝基面
AL, 7, 8, 9, 10
AL, 1, 3, 2, 10
AL, 11, 18, 12, 17
AL, 12, 4, 13, 3
AL, 13, 15, 14
AL, 6, 19, 25, 18
AL, 25, 5, 20, 4
AL, 20, 16, 21, 15
AL, 21, 24, 23, 22

! 8. 定义线网格密度
*DO, I, 1, 6
    LESIZE, I, , ,10
*ENDDO
*DO, I, 7, 10
    LESIZE, I, , , 2
*ENDDO
*DO, I, 11, 16
    LESIZE, I, , , 5
*ENDDO
*DO, I, 17, 24
    LESIZE, I, , , 15
*ENDDO
LESIZE, 25, , , 15

! 9. 划分面网格
MSHKEY, 0    ! 面自由网格划分
TYPE, 1      ! 单元类型
MAT, 1       ! 材料类型
AMESH, 1, 2  ! 划分重力坝面网格
TYPE, 1
MAT, 2
AMESH, 3, 5, 1 ! 划分粘土面网格
TYPE, 1
MAT, 3
AMESH, 6, 9, 1
EPLOT

! 10. 定义位移边界条件
NSEL, S, LOC, X, -210
NPLOT
D, ALL, UX,
ALLSEL, ALL
NSEL, S, LOC, X, 180
NPLOT
D, ALL, UX
ALLSEL, ALL
NSEL, S, LOC, Y, -140
NPLOT
D, ALL, ALL
ALLSEL, ALL

! 11. 对坝基施加竖向荷载
LPLOT
/PNUM, LINE, ON
/REPLOT
SFL, 17, PRES, 670000
SFL, 14, PRES, 450000
SFL, 22, PRES, 450000

! 12. 对上游坝体施加侧向水压力
SFL, 2, PRES, , 670000

! 13. 定义局部坐标系
LOCAL, 11, 0, , , , 39.89

! 14. 选择将被施加水压力的下游坝体节点
NSEL, S, LOC, X, -0.001, 0.001
NSEL, R, LOC, Y, 0, 58.65
NPLOT

! 15. 下游坡面施加水压力荷载
SFGRAD, PRES, 11, Y, 0, -7673
SF, ALL, PRES, 4500000
ALLSEL

! 16. 转换几何模型荷载到有限元模型
SFTRAN

! 17. 显示面荷载
/PSF, PRES, NORM, 2, 0, 1
/REPLOT

! 18. 转换局部坐标系为整体坐标系
CSYS, 0

! 19. 定义重力加速度
ACEL, ,9.8

! 20. 保存有限元模型
SAVE
FINISH

! 21. 进入求解器模块
/SOLU

! 22. 求解当前荷载步
SOLVE
FINISH

! 23. 进入通用后处理模块
/POST1

! 24. 绘制变形形状
PLDISP, 2

! 25. 绘制位移以及应力等值线图
PLNSOL, U, SUM, 0, 1.0
PLNSOL, S, EQV, 0, 1.0
