FINISH $ /CLEAR
/PREP7
! ELEMENT TYPE
ET, 1, BEAM188
KEYOPT, 1, 3, 3    ! CUBIC SHAPE FUNCTION
ET, 2, MASS21
KEYOPT, 2, 3, 2    ! MASS WITHOUT TORATIAL INERTIA
R, 1, 1000
! MATERIAL
MP, EX, 1, 2E11
MP, PRXY, 1, 0.3
! SECTION
SECTYPE, 1, BEAM, ASEC
SECOFFSET, CENT
SECDATA, 0.0311, 0.0038, 0, 0.0038, 0, 0.0076, 0, 0, 0, 0, 1.0, 1.0
! NODE
N, 1,
N, 7, , 18.0
FILL
! ELEMENT: BEAM188
E, 1, 2
EGEN, 6, 1, ALL
! ELEMENT: MASS21
TYPE, 2
*DO, I, 2, 7
    E, I
*ENDDO
! CONSTRINT
D, 1, ALL,
D, ALL, UZ,
FINISH

! MODAL SOLUTION
/SOLU
ANTYPE, MODAL
MODOPT, LANB, 6
MXPAND, 6, , , YES    ! CALCULATE ELEMENT RESULTS
SOLVE
FINISH

/POST1
SET, LIST
FINISH

! 取结构的第1阶自振频率f1和第6阶自振频率f6，根据阻尼比2%计算质量阻尼系数
! 刚度阻尼系数如下：
! ALPHA = 2*pi*f1*f6*\xi/(f1+f6)=0.549
! BETA = \xi/pi/(f1+f6)=0.0000559

! TRANS SOLUTION
/SOLU
ANTYPE, TRANS
NN = 1559    ! 时程曲线上有NN个点
*DIM, AC_X,ARRAY,NN
*VREAD, AC_X(1), wave, csv
(1F8.5)
TRNOPT, FULL
ALPHAD, 0.549
BETAD, 0.0000559
*DO, I, 1, NN
    ACEL, AC_X(1)
    TIME, 0.02*I
    OUTRES, ALL, ALL
    SOLVE
*ENDDO
FINISH

/POST26
NSOL, 2, 7, U, X
PLVAR, 2
